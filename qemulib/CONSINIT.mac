.* Copyright (C) 2023 Harold Grovesteen
.*
.* This file is part of SATK.
.*
.*     SATK is free software: you can redistribute it and/or modify
.*     it under the terms of the GNU General Public License as published by
.*     the Free Software Foundation, either version 3 of the License, or
.*     (at your option) any later version.
.*
.*     SATK is distributed in the hope that it will be useful,
.*     but WITHOUT ANY WARRANTY; without even the implied warranty of
.*     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
.*     GNU General Public License for more details.
.*
.*     You should have received a copy of the GNU General Public License
.*     along with SATK.  If not, see <http://www.gnu.org/licenses/>.

         MACRO
&LABEL   CONSINIT &MODE=WO,&SPIF=
.* CONSINIT calls the SPIF high-level routine that performs low-level
.* interface initialization.
.*
.* Label Field Usage:
.*    The optional &LABEL field is associated with the first generated byte
.*    of the macro.
.*
.* Positional Parameters: None
.*
.* Keyword Parameters:
.*    MODE identifies the ASCII console mode required by the program.
.*           - RW == read/write, both read and write events required.
.*           - WO == write-only, only write events are required.
.*         If omitted WO is the default.
.*    SPIF identifies the label of associated with the SPIF macro.  If omitted
.*         the program must load, prior to execution of CONSINIT macro, the
.*         address of the SPIF macro into general register 1.
.* 
         GBLB  &ISQEMU      Whether this is a Qemu targeted assembly
         LCLB  &NEED1       Whether macro needs to load general register 1
         LCLC  &SETMODE     The hex value use by this console mode
         AIF   (&ISQEMU).QEMUOK
         MNOTE 1,'CONSINIT - MUST BE PRECEDED BY THE QEMU MACRO - MISSING'
         MEXIT
.QEMUOK  ANOP
         AIF   ('&SPIF' EQ '').NONEED1
&NEED1   SETB  1
.NONEED1 ANOP
.*
.* Determine Mode
         AIF   ('&MODE' EQ 'RW').SETRW
         AIF   ('&MODE' EQ 'WO').SETWO
         MNOTE 1,'CONSINIT - MODE PARAMETER INVALID - ''&MODE'''
         MEXIT
.SETRW   ANOP
&SETMODE SETC 'SPIFCMRW'
         AGO  .CKLBL
.SETWO   ANOP
&SETMODE SETC 'SPICFMWO'
.CKLBL   ANOP
         AIF   ('&LABEL' EQ '').NOLBL
&LABEL   DS    0H
         AGO   .LOAD1
.NOLBL   ANOP
         DS    0H
.*
.* Load register 1 if needed
.LOAD1   ANOP
         AIF   (NOT &NEED1).R1_OK
         LARL  1,&SPIF     Locate the SPIF macro containing the SPIFCB
.R1_OK   ANOP
         USING SPIFCB,1   
.* This addressability remains in effect following the macro.  The program
.* can DROP 1 or not as the needs of the program dictate.
.*
.* CONSINIT routine parameter now determined.
         MVI   SPIFCMOD,&SETMODE   Set requested mode in SPIFCB
.*
.* Call the CONSINIT routine
         LA    13,SPIFPGSA   Set the save area address for CONSINIT routine
         LG    15,SPIFHLCI   Fetch the routine address from the SPIFCB
         LGCALL ,            Call the SPIF high-level routine
         MEND
