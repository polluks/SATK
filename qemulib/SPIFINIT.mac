.* Copyright (C) 2023 Harold Grovesteen
.*
.* This file is part of SATK.
.*
.*     SATK is free software: you can redistribute it and/or modify
.*     it under the terms of the GNU General Public License as published by
.*     the Free Software Foundation, either version 3 of the License, or
.*     (at your option) any later version.
.*
.*     SATK is distributed in the hope that it will be useful,
.*     but WITHOUT ANY WARRANTY; without even the implied warranty of
.*     MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
.*     GNU General Public License for more details.
.*
.*     You should have received a copy of the GNU General Public License
.*     along with SATK.  If not, see <http://www.gnu.org/licenses/>.

         MACRO
&LABEL   SPIFINIT &SPIF=,&SCCB=
.* SPIFINIT calls the SPIF high-level routine that performs low-level
.* interface initialization.
.*
.* Label Field Usage:
.*    The optional &LABEL field is associated with the first generated byte
.*    of the macro.
.*
.* Positional Parameters: None
.*
.* Keyword Parameters:
.*    SPIF identifies the label of associated with the SPIF macro.  If omitted
.*         the program must load, prior to execution of SPIFINIT macro, the
.*         address of the SPIF macro into general register 1.
.*    SCCB Identifies the general register containing an address marking the
.*         location following which the SCCB may be located.  If omitted,
.*         the program must have loaded, prior to execution of SPIFINIT,
.*         this address into general register 3.  If preloading general
.*         register 3, the SCCB keyword should be omitted.
.* 
         GBLB  &ISQEMU      Whether this is a Qemu targeted assembly
         LCLB  &NEED1       Whether macro needs to load general register 1
         LCLB  &NEED3       Whether macro needs to load general register 3
         AIF   (&ISQEMU).QEMUOK
         MNOTE 1,'SPIF - MUST BE PRECEDED BY THE QEMU MACRO - MISSING'
         MEXIT
.QEMUOK  ANOP
         AIF   ('&SPIF' EQ '').NONEED1
&NEED1   SETB  1
.NONEED1 ANOP
         AIF   ('&SCCB' EQ '').NONEED3
         AIF   ('&SCCB' EQ '3' OR '&SCCB' EQ 'R3').NONEED3
.* Note if some other symbol is used for R3, an unneeded LGR will be generated
&NEED3   SETB  1
.NONEED3 ANOP
         AIF   ('&LABEL' EQ '').NOLBL
&LABEL   DS    0H
         AGO   .LOAD1
.NOLBL   ANOP
         DS    0H
.*
.* Load register 1 if needed
.LOAD1   ANOP
         AIF   (NOT &NEED1).R1_OK
         LARL  1,&SPIF     Locate the SPIF macro containing the SPIFCB
.R1_OK   ANOP
         USING SPIFCB,1   
.* This addressability remains in effect following the macro.  The program
.* can DROP 1 or not as the needs of the program dictate.
.*
.* Load register 3 if needed
         AIF   (NOT &NEED3).R3_OK
         LGR   3,&SCCB     Set the SCCB suggested address
.R3_OK   ANOP
.* SPIFINIT routine parameters now set.  Can call the routine
.*
.* Call the SPIFINIT routine
         LA    13,SPIFPGSA   Set the save area address for SPIFINIT routine
         LG    15,SPIFHLIA   Fetch the routine address from the SPIFCB
         LGCALL ,            Call the SPIF high-level routine
         MEND
